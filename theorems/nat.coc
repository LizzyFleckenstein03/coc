include base.coc;

def oneplusone := (eq_refl nat 2)
    : eq nat (add 1 1) 2;

def add_assoc := fun (a b c : nat) =>
        ind nat (fun (a : nat) => eq nat (add a (add b c)) (add (add a b) c))
            (eq_refl nat (add b c))
            (fun (a : nat) =>
                eq_ext nat nat S (add a (add b c)) (add (add a b) c))
            a
    : forall (a b c : nat), eq nat (add a (add b c)) (add (add a b) c);

def add_0_r := fun n : nat =>
        ind nat (fun (n : nat) => eq nat n (add n 0))
            (eq_refl nat 0)
            (fun (n : nat) =>
                eq_ext nat nat S n (add n 0))
            n
    : forall (n : nat), eq nat n (add n 0);

def add_S_r := fun a b : nat =>
        ind nat (fun (a : nat) => eq nat (S (add a b)) (add a (S b)))
            (eq_refl nat (S b))
            (fun (a : nat) =>
                eq_ext nat nat S (S (add a b)) (add a (S b)))
            a
    : forall (a b : nat), eq nat (S (add a b)) (add a (S b));

def add_comm := fun a b : nat =>
        ind nat (fun (a : nat) => eq nat (add a b) (add b a))
            (add_0_r b)
            (fun (a : nat) (IH : eq nat (add a b) (add b a)) =>
                eq_trans nat
                    (S (add a b)) (S (add b a)) (add b (S a))
                    (eq_ext nat nat S (add a b) (add b a) IH)
                    (add_S_r b a))
            a
    : forall (a b : nat), eq nat (add a b) (add b a);
